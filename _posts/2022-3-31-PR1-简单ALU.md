---
title: 简单ALU设计
tags: C-课堂笔记 Verilog COD-ALU
key: COD-ALU
---

这学期的计算机组成原理要求我们用Verilog实现一个简单的CPU，第一阶段，我们需要考虑一个简单的ALU和一个寄存器。

<!--more-->
<!-- {% raw %} -->
## 1 32x32寄存器

我们只需要一个“ 2读1写” ：两个读端口 + 一个写端口。

32× 32-bit寄存器堆的输入输出端口定义如下：

![端口定义](https://raw.githubusercontent.com/Richard-dick/richard-dick.github.io/main/_posts/inc/ALU1.png)

有一个小**要求**：

- 仅当wen=1（有效）且waddr不等于0时，才可以向waddr对应的寄存器写入wdata

```verilog
`timescale 10 ns / 1 ns
`define DATA_WIDTH 32
`define ADDR_WIDTH 5
`define REG_NUM 32

module reg_file(
	input                       clk,
	input  [`ADDR_WIDTH - 1:0]  waddr,
	input  [`ADDR_WIDTH - 1:0]  raddr1,
	input  [`ADDR_WIDTH - 1:0]  raddr2,
	input                       wen,
	input  [`DATA_WIDTH - 1:0]  wdata,
	output [`DATA_WIDTH - 1:0]  rdata1,
	output [`DATA_WIDTH - 1:0]  rdata2
);
	reg [`DATA_WIDTH-1:0]register[`REG_NUM-1:0];
	always @(posedge clk) begin
		if(wen == 1 && waddr != 0)
			register[waddr] <= wdata;
		//else  ;这里是为了保证模块内部的“纯净”
		//register[0] <= 32'b0;
	end
	
	assign rdata1 = (raddr1 != 0)?register[raddr1]:(32'b0);
	assign rdata2 = (raddr2 != 0)?register[raddr2]:(32'b0);
endmodule
```

## 2 简单ALU

其端口定义如下：

![ALU端口定义](https://raw.githubusercontent.com/Richard-dick/richard-dick.github.io/main/_posts/inc/ALU2.png)

各种操作须知：

![定义](https://raw.githubusercontent.com/Richard-dick/richard-dick.github.io/main/_posts/inc/ALU3.png)

### 2.1 代码实现解释

首先是两个逻辑运算：

```verilog
assign Res_and = A&B; // 直接按位与
assign Res_or = A|B; //直接按位或
```

接着是考虑两个逻辑结果的取舍：

```verilog
assign Mid_logic = ( (Res_and &{32{~ALUop[0]}}) | (Res_or &{32{ALUop[0]}}) )&{32{~ALUop[1]}};
```

这里是考虑`ALUop[0]`的不同，进行一个位扩展+与运算。

接下来是算术运算的核心：**在计算机中，只有补码和无符号数两种存储方式**。

#### 2.1.1 无符号数解释

同样的，和原码做补码代替减法一样，不过对无符号数的运算不太一样。它称为取补。核心运算图如下：

![unsigned](https://raw.githubusercontent.com/Richard-dick/richard-dick.github.io/main/_posts/inc/ALU4.png)

`-0011`的取补运算就是按位取反再加1，得到`1101`，我们在图上考虑这些事情，观察取补运算的核心，这样就可以很方便地得到进位和借位的判断。

我们考虑任何一个无符号数。默认箭头朝右，当作为减数的时候，就将箭头倒向左。这时候补运算的核心就凸显出来了。

直接取一个向右的无符号数与减数的真值互补即可。这时候，如果减数较小，那么其长度小于被减数，所以其补要大于被减数离`10000`的差距。所以取补加起来必然会产生进位。

反过来，如果减数较大，那么其补要小于差距，所以取补加起来不会产生进位。

得到**无符号数做减法时，进位1为正常，否则就为借位。而做简单的加法则相反**。对于算术ALUop来说，差距在`ALUop[2]`上，这也就完成了对`CarryOut`的解释。

```verilog
assign CarryOut = ALUop[2]^prob_cout;
```

#### 2.1.2 有符号数解释(补码)

接下来考虑有符号数，这里我们直接越过原码的思维。

切记：**在理解计算机存储数字的路程上，原码只是一个小小的跳板，补码才是核心，原码时一个"多余"的东西，建议直接抛弃。**

对补码的核心理解是**最高位的理解**，它的权值是$-2^{n-1}$。那么用同样的图可知：

![2's complement](https://raw.githubusercontent.com/Richard-dick/richard-dick.github.io/main/_posts/inc/ALU5.png)

按`0011`取补得到`1101`，从中可以看出补码加法的朴实：

- 如果正数大，那么后三位进位就把**负权值的高位**消掉了，如果负数大就消不掉，但是可以使正值部分增加，从而让负数变大。

接下来看两种**溢出情况**：

- 如果两正数产生了负权值的进位，那么就必然是溢出；
- 如果两负数的负权值位相加进位被抹去，而正值位的进位没有补上，就会得到正数，那么也属于溢出。

> 其上的情况都可以在图上表示出。

最后从图上可以看出**取补运算移动**的核心。

最终得到了：

```verilog
assign Overflow = ((~A[31])&(~B[31])&Res_cal[31]&(~ALUop[2])) 
| ((A[31])&(B[31])&(~Res_cal[31])&(~ALUop[2])) 
| ((A[31])&(~B[31])&(~Res_cal[31])&(ALUop[2])) 
| ((~A[31])&(B[31])&(Res_cal[31])&(ALUop[2]));
//就是把四种情况单独拿出来
```

从上我们得到了**取补运算的合理性**，所以用朴素的选择语句：

```verilog
assign {prob_cout,Res_cal} = A+( (B^{32{ALUop[2]}})+{{31{1'b0}},{ALUop[2]}} );
```

> 这里提一句和补码变化的区别；<br>
> 可以看出取补运算按位取反+1得到的结果相当于1111-$x_3x_2x_1x_0+1$，相当于$1\ 0000-x_3x_2x_1x_0$<br>
> 而补码运算则考虑了原码符号位的问题，还需要把符号位消去后，再按位取反再+1，所以是$10\ 0000-x_3x_2x_1x_0$

其余的信号和选择都比较好理解，从而得到以下的所有代码：

### 3 代码示例

```verilog
`timescale 10 ns / 1 ns

`define DATA_WIDTH 32

module alu(
	input  [`DATA_WIDTH - 1:0]  A,
	input  [`DATA_WIDTH - 1:0]  B,
	input  [              2:0]  ALUop,
	output                      Overflow,
	output                      CarryOut,
	output                      Zero,
	output [`DATA_WIDTH - 1:0]  Result
);
	// TODO: Please add your logic design here
	// 由于线路过于复杂，所以用几个中间变量进行稍微的简化。
	wire [`DATA_WIDTH-1:0] Res_and,Res_or,Res_cal;
	wire [`DATA_WIDTH-1:0] Mid_logic, Mid_cal;
	wire prob_cout;

	// 之后以test为名的信号都是用来测试的，对运算优先级的考察和中间结果运算正误判断起到了积极作用。
	//wire [`DATA_WIDTH-1:0] test,test_1,test_2; 

	assign Res_and = A&B; // 直接按位与
	assign Res_or = A|B; //直接按位或
	assign {prob_cout,Res_cal} = A+( (B^{32{ALUop[2]}})+{{31{1'b0}},{ALUop[2]}} );//这里是根据ALUop的位数不同来选择是对B进行取补运算。
	
	//这里考虑溢出OF，也就是有符号数的问题。
	// 直接考察四种情况，还可以对这四种情况写标准bool式。这里就不化简了。
	//  0 + 0 = 1; 只对一个式子做解释，也就是正数+正数却得到了负数。
	//  1 + 1 = 0;
	//  1 - 0 = 0;
	//  0 - 1 = 1;
	assign Overflow = ((~A[31])&(~B[31])&Res_cal[31]&(~ALUop[2])) | ((A[31])&(B[31])&(~Res_cal[31])&(~ALUop[2])) | ((A[31])&(~B[31])&(~Res_cal[31])&(ALUop[2])) | ((~A[31])&(B[31])&(Res_cal[31])&(ALUop[2]));
	
	// 考虑进位/借位，直接观察+法中的进位即可；例外的情况是当作减法来看的情况，这种情况的溢出体现在最高位的0上。因为“不够减”。
	assign CarryOut = ALUop[2]^prob_cout;
	
	// 考虑逻辑的最终输出结果。只需要对ALUop第二位的不同做分析即可。
	assign Mid_logic = ( (Res_and &{32{~ALUop[0]}}) | (Res_or &{32{ALUop[0]}}) )&{32{~ALUop[1]}};

	// considering the logic part-> 这里只是纠错的中途步骤
	// assign test = (B^{32{ALUop[2]}}) + {{31{1'b0}},{ALUop[2]}};
	// assign test_1 =  B^{32{ALUop[2]}};
	// assign test_2 = {{31{1'b0}},{ALUop[2]}};

	// 同理，也需要考虑ALUop的第二位来判断是否有计算输出；但不同的是，还需要考虑SLT的情况，也就是一个取或，本质上是根据ALUop第三位的01判断是输出Res_cal或者单纯的SLT结果
	// 也就是(Res_cal[31]^Overflow)，比大小的过程中也需要考虑Overflow，如果其为0，则只需要看高位即可，否则还需要考虑它。
	assign Mid_cal = ( (Res_cal & (  { 32{~ALUop[0]} }  )) | {{31{1'b0}},{(ALUop[0]&(Res_cal[31]^Overflow))}} ) & {32{ALUop[1]}};
	
	// 最后对两个结果进行一个选择即可。
	assign Result = (Mid_logic|Mid_cal);
	assign Zero = (~(| Result));
endmodule
```



<!-- {% endraw %} -->



