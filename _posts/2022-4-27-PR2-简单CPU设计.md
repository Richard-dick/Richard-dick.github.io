---
title: 简单CPU设计
tags: C-课堂笔记 Verilog COD-CPU
key: COD-CPU
mathjax: true
typora-root-url: ..
---

本文只讲述一个简单的，单周期的，简陋的，用verilog语言描述的CPU罢了。

被逼无奈，还是写了一个多周期的简单CPU。

<!--more-->
<!-- {% raw %} -->
## 1 修改ALU

更新操作如下：

<center><img src="/assets/images/posts/Past/CPU1.png" style="zoom: 70%;" /></center>

对于无符号数，见[ALU](https://richard-dick.github.io/2022/03/31/PR1-%E7%AE%80%E5%8D%95ALU.html)

alu代码如下：

```verilog
......
module alu(
	......
);
	// TODO: Please add your logic design here
	// 由于线路过于复杂，所以用几个中间变量进行稍微的简化。
	wire [`DATA_WIDTH-1:0] Res_and,Res_or,Res_cal,Res_xor,Res_nor;
	wire [`DATA_WIDTH-1:0] Mid_logic, Mid_cal;
	wire Mid_comp;
	wire prob_cout;

	// 之后以test为名的信号都是用来测试的，对运算优先级的考察和中间结果运算正误判断起到了积极作用。
	//wire [`DATA_WIDTH-1:0] test,test_1,test_2; 

	assign Res_and = ( (A&B) &{32{~ALUop[0]}} ); 	// 直接按位与
	assign Res_or = (A|B) & {32{ALUop[0]}}; 		//直接按位或
	assign Res_xor = (A^B) &{32{~ALUop[0]}};	//直接按位异或
	assign Res_nor = (~(A|B)) & {32{ALUop[0]}};	//对或信号取反
	assign {prob_cout,Res_cal} = A+( (B^{32{ALUop[2] | ALUop[0]}})+{{31{1'b0}},{ALUop[2] | ALUop[0]}} );//这里是根据ALUop的位数不同来选择是对B进行取补运算。
	
	//......
	assign Overflow = ((~A[31])&(~B[31])&Res_cal[31]&(~ALUop[2])) | ((A[31])&(B[31])&(~Res_cal[31])&(~ALUop[2])) 
		| ((A[31])&(~B[31])&(~Res_cal[31])&(ALUop[2])) | ((~A[31])&(B[31])&(Res_cal[31])&(ALUop[2]));
	
	// 考虑进位/借位，直接观察+法中的进位即可；例外的情况是当作减法来看的情况，这种情况的溢出体现在最高位的0上。因为“不够减”。
	assign CarryOut = ALUop[2]^prob_cout;
	
	// 考虑逻辑的最终输出结果。只需要对ALUop第二位的不同做分析即可。现在需要对ALUop[0]和2进行分析。
	//而且也要加一个选择了；
	assign Mid_logic = ( (( Res_and|Res_or )  & {32{~ALUop[2]}} )
		| (( Res_xor|Res_nor ) & {32{ALUop[2]}} ))   & {32{~ALUop[1]}} ;

	// 同理，也需要考虑ALUop的第二位来判断是否有计算输出；
	//考虑SLT的情况，也就是一个取或，本质上是根据ALUop第三位的01判断是输出Res_cal或者单纯的comp结果
	//而对于SLTU，无符号数。有进位，就是A>=B；无进位则是A<B;
	//也就是，有进位，prob_cout=1;A>=B,输出0
	//无进位，prob_cout=0,A<B,输出1.
	// 也就是(Res_cal[31]^Overflow)，比大小的过程中也需要考虑Overflow，如果其为0，则只需要看高位即可，否则还需要考虑它。
	// 比较复杂，重新弄个一个Mid_comp;
	//assign Mid_cal = ( (  Res_cal & ( { 32{~ALUop[0]} } )  ) | {{31{1'b0}},{(ALUop[0]&(Res_cal[31]^Overflow))}} ) & {32{ALUop[1]}};
	assign Mid_comp = ( (Res_cal[31]^Overflow) & ALUop[2] )
			| ( ~(prob_cout | ALUop[2]) ) ;
	......
endmodule
```

## 2 添加Shifter

<center><img src="/assets/images/posts/Past/CPU2.png" style="zoom: 100%;" /></center>

由于Verilog本身移位需要考虑signed之类的麻烦，故采用通式移位器：

```verilog
`timescale 10 ns / 1 ns
`define DATA_WIDTH 32
module shifter (
	input  [`DATA_WIDTH - 1:0] A,
	input  [              4:0] B,
	input  [              1:0] Shiftop,
	output [`DATA_WIDTH - 1:0] Result
);
	// TODO: Please add your logic code here
	//设置Res_left作为左移结果，Res_right作为右移结果；
	//设置Complement为移位补全位；
	wire [`DATA_WIDTH - 1:0] Res_left, Res_right;
	wire [`DATA_WIDTH - 1:0] Res_left_1,Res_left_2,Res_left_3,Res_left_4;
	wire [`DATA_WIDTH - 1:0] Res_right_1,Res_right_2,Res_right_3,Res_right_4;
	wire Complement;
	
	//左移Lop就是Shiftop[0]，直接补零即可
	//右移则是在Shiftop[0]的基础上，看Shiftop[1]&A[31]后。而且，补1的唯一情况就是算术移位+最高位是1；
	assign Complement = (&Shiftop)&A[31];
	
	//最后只需要考虑左右移了。采用桶式移位器！
	//桶式移位器，可以使用一个变量名选择到底；另外，assign可以直接声明变量，不过位长会跟不同IDE的实现有关。
	assign Res_left_1 = (B[4]==1)?({{A[15:0]},{16{Complement}}}):(A);
	assign Res_left_2 = (B[3]==1)?({{Res_left_1[23:0]},{8{Complement}}}):(Res_left_1);
	assign Res_left_3 = (B[2]==1)?({{Res_left_2[27:0]},{4{Complement}}}):(Res_left_2);
	assign Res_left_4 = (B[1]==1)?({{Res_left_3[29:0]},{2{Complement}}}):(Res_left_3);
	assign Res_left = (B[0]==1)?({{Res_left_4[30:0]},{1{Complement}}}):(Res_left_4);
	
	assign Res_right_1 = (B[4]==1)?({{16{Complement}},{A[31:16]}}):(A);
	assign Res_right_2 = (B[3]==1)?({{8{Complement}},{Res_right_1[31:8]}}):(Res_right_1);
	assign Res_right_3 = (B[2]==1)?({{4{Complement}},{Res_right_2[31:4]}}):(Res_right_2);
	assign Res_right_4 = (B[1]==1)?({{2{Complement}},{Res_right_3[31:2]}}):(Res_right_3);
	assign Res_right = (B[0]==1)?({{1{Complement}},{Res_right_4[31:1]}}):(Res_right_4);
	
	assign Result = (Shiftop[1]==1)?(Res_right):(Res_left);
	
endmodule
```

## 3 设计CPU

接下来就涉及到CPU的设计了。

CPU实验的核心在以下几个方面：

- 译码
  - 不能逐条译码，不够优雅；
- 操作
  - 尤其要注意内存寄存器堆的读写
- 数据通路

以下是各**端口**的要求：

<center><img src="/assets/images/posts/Past/CPU3.png" style="zoom: 55%;" /></center>

我们首先来尝试译码：

### 3.1 译码

先翻译R-type指令：

<center><img src="/assets/images/posts/Past/CPU5.png" style="zoom: 60%;" /></center>

从后面可以看出，大部分寄存器的位置都是固定的。

采用一个R_type的信号作为标志，之后也是类似，由此可完成大多数译码。

```verilog
//1.把R-type译出来；
    //R-高度一致的合并代码
	wire R_type = (opcode == 6'b000000);//所有R_type都会产生寄存器写
	//reg_raddr1/wire [4:0] R_raddr1 = rs;
	//reg_raddr2/wire [4:0] R_raddr2 = rt;
	//RegDst/wire [4:0] R_waddr  = rd;
   
    //运算指令
    wire R_calc = R_type & func[5];//只有opcode都是0时，且func高位是1，Rtype才是1;写入alu_result
    wire [2:0] R_alu_op = {6{R_calc}} & //只有R为真，才是直接使用的alu
                        (func[2]?{func[1],!func[2],func[0]}://逻辑运算译码
                        (!func[3]?{func[1],!func[2],!func[0]}:{!func[0],!func[2],func[1]})//slt有一点特殊
                        );//
	
	//移位指令, 考虑到只有它在用移位器，于是
	wire R_shift = R_type&(func[5:3] == 3'b000);
	assign Shifter_Shiftop = func[1:0];
	wire variable = R_shift & func[2];//考虑是否使用移位变量；
	assign Shifter_A = reg_rdata2;
	assign Shifter_B = variable?reg_rdata1[4:0]:sa;
	//RF_wdata/wire [31:0] R_shift_result = Shifter_Result;//写入Shifter_Result
	
	//跳转指令
	wire R_jump = R_type&&(func[5:1] == 5'b00100);
	wire [31:0] Rj_JumpAddr = reg_rdata1;//感觉此处并不需要特别地处理func[0]，可能是到后面选择的时候用吧。
	wire Rj_ret = R_jump&func[0];//写入pc_8
	//RF_wdata/wire [31:0] Rj_wdata = pc_8;
	wire R_wen = R_jump?func[0]:R_type;
	
	//mov指令
	wire R_mov = R_type&&(func[5:1] == 5'b00101);
	wire rt_Zero = ~(|reg_rdata2);//如果rt读出数据是0，那么Zero就是1
	wire mov = R_mov&(rt_Zero^func[0]);//此时就可以靠异或来得到是否mov，此处也是在后面用到的信号。
	//RF_wdata/wire [31:0] Rm_wdata = reg_rdata1;//写入reg_rdata1
```

需要注意的是，Jr指令是不讲wen拉高的。而jalr是拉高的。

### 3.2 逐条实践

这里也简单说明几条难实现的指令。

#### 3.2.1 IL指令

lb,lh,lbu,lhu这四条指令都是读出reg，然后将他们放入内存写数据的低位。然后该怎么扩展怎么扩展。

**lwl和lwr则是非同常人能设想出的指令，他在大端和小端系统完全不同。**

lwl:To load the most-signiﬁcant part of a word as a signed value from an unaligned memory address

从内存中取出数据，也就是载入到内存中最为重要的字节上。一般来说，我们把左边视作MSB，所以是lw-left。

<center><img src="/assets/images/posts/Past/CPU6.png" style="zoom: 60%;" /></center>

考虑数据IJKL，其中I是最重要的，按照大小端：

- big-endian：I被置于小地址；
- little-endian：I被置于大地址。

> 大小端可以考虑小地址处的数据。大端则是存最重要的；小端则是存不重要的。

**先考虑小端的情况：**

我们看，当地址尾巴为11时，我们向低地址区读入，所以是将mem读入的全部内容载入reg。

而00时，也是向低地址区，所以只会读入最低的L进入reg，当然是当作最高位来处理的。

**再考虑大端：**

它是向高地址对齐，所以11只读入一位；而00读入全部。

从这个意义上来看，它载入的方向都是向着LSB的方向读入的。

所以LWL的全部阐述为：按地址访存，将当前地址到LSB地址中间的内容全部取出，作为高位放入reg中。

```verilog
wire [31:0] IL_mask_rl_data = //前面是lwr，
	(ILS_Address[1:0]==2'b11 ?(opcode[2]?{reg_rdata2[31:8],IL_read_data[31:24]}:IL_read_data)
	:(ILS_Address[1:0]==2'b10 ?(opcode[2]?{reg_rdata2[31:16],IL_read_data[31:16]}:{IL_read_data[23:0],reg_rdata2[7:0]})
	:(ILS_Address[1:0]==2'b01 ?(opcode[2]?{reg_rdata2[31:24],IL_read_data[31:8]}:{IL_read_data[15:0],reg_rdata2[15:0]})
	:(opcode[2]?IL_read_data:{IL_read_data[7:0],reg_rdata2[23:0]}))));
```

这时考虑LWR则比较轻松，只不过方向是向着MSB方向取数。

由于是小端，11时就只读入一位作为最低位了。

那么完全的叙述为：

LWR：按地址访存，将当前地址到MSB地址的内容全部取出，作为低位放入目标reg。

#### 3.2.2 IS指令

sb，sh，sw则是根据计算出的地址进行选择。

**sb：00是mask为0001，数据也是放入最低位。需要注意的是，放入的总是内存中的低位。**

```verilog
wire [3:0] IS_mask_b_strb = 
	(ILS_Address[1:0]==2'b00 ?4'b0001
    	:(ILS_Address[1:0]==2'b01 ?4'b0010
    	:(ILS_Address[1:0]==2'b10 ?4'b0100:4'b1000)));
wire [31:0] IS_b_data = 
	(ILS_Address[1:0]==2'b00 ? {{24{1'b0}},reg_rdata2[7:0]}
        :(ILS_Address[1:0]==2'b01 ?{{16{1'b0}},reg_rdata2[7:0],{8{1'b0}}}
        :(ILS_Address[1:0]==2'b10 ?{{8{1'b0}},reg_rdata2[7:0],{16{1'b0}}}
        :{reg_rdata2[7:0],{24{1'b0}}})));
```

**sh：放入的总是内存低位**

```verilog
wire [3:0] IS_mask_h_strb = ILS_Address[1:0]==2'b10 ?4'b1100:4'b0011;
wire [31:0] IS_h_data = (ILS_Address[1:0] == 2'b10)?{reg_rdata2[15:0],{16{1'b0}}}:{{16{1'b0}},reg_rdata2[15:0]};
```

**swl：To store the most-significant part of a word to an unaligned memory address**

<center><img src="/assets/images/posts/Past/CPU7.png" style="zoom: 60%;" /></center>

同样的，我们先考虑小端，他是从reg的高位读入。然后考虑内存是向LSB读。

在这一点是，-wl都是一致的。均是从reg的最高位下手，然后向着LSB方向写入。

swl：按地址访存，将当前地址到LSB地址中间的内容全部取出，替换成reg的高位。

<center><img src="/assets/images/posts/Past/CPU8.png" style="zoom: 60%;" /></center>

**swr：To store the least-significant part of a word to an unaligned memory address**

那么SWR的作用也容易揭晓：按地址访存，将当前地址到MSB地址中间的内容全部取出，替换成reg的低位。


```verilog
wire [3:0] IS_mask = //前面是swr
	(ILS_Address[1:0]==2'b11 ?(opcode[2]?4'b1000:4'b1111)
	:(ILS_Address[1:0]==2'b10 ?(opcode[2]?4'b1100:4'b0111)
	:(ILS_Address[1:0]==2'b01 ?(opcode[2]?4'b1110:4'b0011)
	:(opcode[2]?4'b1111:4'b0001))));
wire [31:0] IS_rl_data = //前面是swr，
	(ILS_Address[1:0]==2'b11 ?(opcode[2]?{reg_rdata2[7:0],{24{1'b0}}}:reg_rdata2)
	:(ILS_Address[1:0]==2'b10 ?(opcode[2]?{reg_rdata2[15:0],{16{1'b0}}}:{{8{1'b0}},reg_rdata2[31:8]})
	:(ILS_Address[1:0]==2'b01 ?(opcode[2]?{reg_rdata2[23:0],{8{1'b0}}}:{{16{1'b0}},reg_rdata2[31:16]})
	:(opcode[2]?reg_rdata2:{{24{1'b0}},reg_rdata2[31:24]}))));
```



### 3.3 数据通路

<center><img src="/assets/images/posts/Past/CPU4.png" style="zoom: 60%;" /></center>

```verilog
//开始构建数据通路
assign RegDst = R_type & (R_mov?(mov?1'b1:1'b0):1'b1);//看看哪些是写入rt，哪些是写rd。还有一个0和特殊的31号。取1，取rd，考虑mov指令的选择，要在0寄存器。
//考虑实际的Write_register, 并注释掉重复写的地方。
assign RF_waddr = RegDst?rd:(//如果是1，就选rd
	            (IL || IC)?rt:(//如果是IL和IC，就选rt
	            (J_type && J_wen)?5'b11111:5'b00000));//如果是JAL，就31，反之则选择0，反正也写不进去。
	                   
//只有RI或者IB，J是直接跳转。不过还需要更细致一点，毕竟还需要Zero的保证。                   
assign Branch = (RI & RI_branch) | (IB & (IB_eq_branch | IB_gl_branch));
//考虑实际的跳转情况，按照之前的构想，我们只需要考虑branch信号和JumpAddr两个信号即可。
assign NEW_PC = Jump?pc_addr:(Branch?pc_branch:pc_4);
assign pc_branch = pc_4 + branch_addr;//IB的跳转目的是一致的。
assign pc_addr = JumpAddr;
//signle cohere/assign branch_addr = RIB_branch_addr;
assign Jump = J_type | R_jump;
assign JumpAddr = J_type?J_next_pc:Rj_JumpAddr;
	
//Mem_reg_op
assign MemWrite = opcode[5] & opcode[3];
assign MemRead = opcode[5] & (~opcode[3]);
assign RF_wdata = (R_calc || IC_wen)?alu_Result:
	      (R_shift?Shifter_Result:
	      ((Rj_ret || J_wen)?pc_8:
	      (mov?reg_rdata1:IL_wdata)));
assign RF_wen = (R_wen | J_wen | IC_wen | IL);//特判一个JR
assign Address = ILS_Address_aligned;
	//assign Write_data = IS_write_data;
	//assign Write_strb = IS_write_strb;
	
	//ALU的解码
assign ALUSrc = (R_calc | RI | IB);//R_calc, RI, IB 三类指令是R+R；IC, IL, IS三类指令是R+imm;1->2R
assign reg_raddr1 = rs; // rs的读取是注定的，纵使有的地方用的是base，不过也是用来作为标号的。其实还有SLL的不同
assign alu_A = reg_rdata1;
assign reg_raddr2 = RI?5'b00000:rt;//rt确实是注定的。
wire [31:0] Imm = (IC?extended_imm:ILS_extended_imm);
assign alu_B = ALUSrc?reg_rdata2:Imm;
assign ALUop = R_calc?R_alu_op:
	               (RI?RI_alu_op:
	               (IB?IB_alu_op:
	               (IC?IC_alu_op:3'b010)));
assign alu_ALUop = ALUop;
```

这就完成了单周期的全部解释。


## 4 多周期CPU

但上述CPU的实用性很弱，这是因为clk的周期被最长的指令操作限制了。

根据**加速大多数**原则，我们尝试把它分成五个部分，那么最长的部分就由小部分决定。

这便是多周期的原理，多周期+流水线的配置是十分自然的，尽管后者的实现十分复杂。

### 4.1 状态机

我们按如下跳转状态图来构造。

<center><img src="/assets/images/posts/Past/CPU9.png" style="zoom: 60%;" /></center>

用verilog实现状态机分为三步：

1. next跳转
2. 状态行为
3. 可能的状态输出

```verilog
/*
 * ============================================================================
 * 1.声明状态和next跳转
 * ============================================================================
 */
    localparam IF       = 5'b00001;
    localparam ID       = 5'b00010;
    localparam EX       = 5'b00100;
    localparam MEM      = 5'b01000;
    localparam WB       = 5'b10000;
    
    reg [4:0] current_state;
    reg [4:0] next_state;
    
    always @(posedge clk)
    begin
        if(rst)
            current_state <= IF;
        else
            current_state <= next_state;
    end
```

这里状态采取本地参量和one-hot编码，这样对应状态只需要取一位便可以完美分别，实用性很强。

```verilog
 /*
 * ===========================================================================
 * //时序部分：包含下一状态选择；PC选择
 * ===========================================================================
 */   
    wire NOP = !(|IR);//用来确认当前译码是否可读，否则直接PC+4进下一跳。
    always @(*)
    begin
        case(current_state)
            IF: begin//无条件进ID
                next_state = ID;
            end
            //译码阶段：若指令有效，则进入执行阶段。
            ID: begin
                if(NOP)  next_state = IF;
                else next_state = EX;
            end
            //执行阶段：进行分流进入三个状态。
            EX: begin
                if(RI | IB | Jp) next_state = IF;//这里没有考虑RJ指令的问题
                else if(R_type | IC | J_wen) next_state = WB;
                else next_state = MEM;
            end
            //内存：从LS中选择
            MEM: begin
                if(IL) next_state = WB;
                else next_state = IF;
            end
            //写回，无条件进IF
            WB: begin
                next_state = IF;
            end
        endcase
    end
```

由于我们实现的是一个十分简单的CPU，所以本来应该放入时序部分的电路部分就暂时归于组合逻辑了。

### 4.2 PC怎么跳？

CPU一个很重要的地方就是PC的正确，它确定了下一条指令的位置。若是错了，则整个程序很难正确。

首先，需要了解时序逻辑的非阻塞赋值：

- 在进入always语句时计算右侧值，然后在下一次进入时，将右侧值统一赋给左侧。

所以这周期的变化，下周期开头才会变化。

```verilog
    //PC 行为--------------------------------------------IMPORTANT
	always@(posedge clk)begin
	   if(current_state[0]) reg_PC <= PC;//if
	   else reg_PC <= reg_PC;
	end
	
	always@(posedge clk)begin
	   if(rst) PC <= 32'b0;
	   else if(current_state[0] & ~rst) PC <= PC + 4;//if
	   else if(current_state[2] & ~rst) PC <= NEW_PC;//ex
	   else PC <= PC;
	end
```

在IF阶段，已经读入指令，所以PC直接跳转+4；等到了EX阶段，然后再考虑新PC是怎样的。

reg_PC是用来存储当前指令的PC值，给JALR指令使用。

### 4.3 储存信息

PC的跳转引来一个问题，也就是IF过后，PC就变化了，而Instruction也会发生变化，这时就需要用一个IR寄存器来存储指令。另外一个需要存储的值就是读进来的内存值。

PC一变化，与之相连的组合逻辑模块便会变化，这就是为什么必须要存储。

```verilog
	always@(posedge clk)begin//IR
	   if(current_state[0] & ~rst) IR <= Instruction;//IF
	   else IR <= IR;
	end
	
	always@(posedge clk)begin//从内存中读出的数据
	   if(current_state[3] & ~rst) mem_data <= Read_data;
	end
```

然后保证寄存器和内存写和读的信号只在对应状态才能拉高即可。


<div style="display:none">
single_cpu代码如下：

```verilog
`timescale 10ns / 1ns

module simple_cpu(
	input             clk,
	input             rst,

	output [31:0]     PC,
	input  [31:0]     Instruction,

	output [31:0]     Address,
	output            MemWrite,
	output [31:0]     Write_data,
	output [ 3:0]     Write_strb,

	input  [31:0]     Read_data,
	output            MemRead
);

	// THESE THREE SIGNALS ARE USED IN OUR TESTBENCH
	// PLEASE DO NOT MODIFY SIGNAL NAMES
	// AND PLEASE USE THEM TO CONNECT PORTS
	// OF YOUR INSTANTIATION OF THE REGISTER FILE MODULE
	wire			RF_wen;
	wire [4:0]		RF_waddr;
	wire [31:0]		RF_wdata;
/* TODO: PLEASE ADD YOUR CODE BELOW
 * ============================================================================================================================
 * /REG实例化,由于一些原因，只能使用它声明的东西。//wire [4:0] reg_waddr;//wire reg_wen;//wire [31:0] reg_wdata;
 * ============================================================================================================================
 */ wire [4:0] reg_raddr1;
	wire [4:0] reg_raddr2;
	wire [31:0] reg_rdata1;
	wire [31:0] reg_rdata2;
	reg_file inst_reg_file(
	   .clk        (clk),
	   .waddr      (RF_waddr),
	   .raddr1     (reg_raddr1),
	   .raddr2     (reg_raddr2),
	   .wen        (RF_wen),
	   .wdata      (RF_wdata),
	   .rdata1     (reg_rdata1),
	   .rdata2     (reg_rdata2)
	);
/*
 * ============================================================================================================================
 * //ALU实例化
 * ============================================================================================================================
 */ wire  [31:0]   alu_A;
	wire  [31:0]   alu_B;
	wire  [2:0]    alu_ALUop;
	wire           alu_Overflow;
	wire           alu_CarryOut;
	wire           alu_Zero;
	wire [31:0]    alu_Result;
	alu inst_alu(
	    .A             (alu_A)
	   ,.B             (alu_B)
	   ,.ALUop         (alu_ALUop)
	   ,.Overflow      (alu_Overflow)
	   ,.CarryOut      (alu_CarryOut)
	   ,.Zero          (alu_Zero)
	   ,.Result        (alu_Result)
	);
/*
 * ============================================================================================================================
 * //Shifter实例化
 * ============================================================================================================================
 */ wire  [31:0]   Shifter_A;
	wire  [4:0]    Shifter_B;
	wire  [1:0]    Shifter_Shiftop;
	wire [31:0]    Shifter_Result;
	shifter inst_shifter(
	    .A             (Shifter_A)
	   ,.B             (Shifter_B)
	   ,.Shiftop       (Shifter_Shiftop)
	   ,.Result        (Shifter_Result)
	);
	//声明一些CU的信号
	wire RegDst;
	wire Branch;
	wire MemtoReg;
	wire [2:0] ALUop;
	wire ALUSrc;
	wire RegWrite;
	wire Jump;
	
/*
 * ============================================================================================================================
 * //说明PC的行为；
 * ============================================================================================================================
 */ wire [31:0] NEW_PC;
    wire [31:0] pc_4 = PC + 4;
    wire [31:0] pc_8 = PC + 8;
    wire [31:0] branch_addr;
    wire [31:0] JumpAddr;
    wire [31:0] pc_branch;
    wire [31:0] pc_addr;
    reg  [31:0] reg_PC;
	always@(posedge clk)begin
	   if(rst) reg_PC <= 32'b0;
	   else reg_PC <= NEW_PC;
	end
	assign PC = reg_PC;

/*
 * ============================================================================================================================
 * //译码。。。先把Instruction拆开
 * ============================================================================================================================
 */
    //拆Instruction
    wire [5:0]  opcode      = Instruction[31:26];
    wire [4:0]  rs          = Instruction[25:21];
    wire [4:0]  rt          = Instruction[20:16];
    wire [4:0]  rd          = Instruction[15:11];
    wire [15:0] imm         = Instruction[15: 0];
    wire [4:0]  sa          = Instruction[10:6];
    wire [5:0]  func        = Instruction[5:0];
    wire [25:0] instr_index = Instruction[25:0];
	
    //1.把R-type译出来；
    //R-高度一致的合并代码
	wire R_type = (opcode == 6'b000000);//所有R_type都会产生寄存器写
	//reg_raddr1/wire [4:0] R_raddr1 = rs;
	//reg_raddr2/wire [4:0] R_raddr2 = rt;
	//RegDst/wire [4:0] R_waddr  = rd;
   
    //运算指令
    wire R_calc = R_type & func[5];//只有opcode都是0时，且func高位是1，Rtype才是1;写入alu_result
    wire [2:0] R_alu_op = {6{R_calc}} & //只有R为真，才是直接使用的alu
                        (func[2]?{func[1],!func[2],func[0]}://逻辑运算译码
                        (!func[3]?{func[1],!func[2],!func[0]}:{!func[0],!func[2],func[1]})//slt有一点特殊
                        );//
	
	//移位指令, 考虑到只有它在用移位器，于是
	wire R_shift = R_type&(func[5:3] == 3'b000);
	assign Shifter_Shiftop = func[1:0];
	wire variable = R_shift & func[2];//考虑是否使用移位变量；
	assign Shifter_A = reg_rdata2;
	assign Shifter_B = variable?reg_rdata1[4:0]:sa;
	//RF_wdata/wire [31:0] R_shift_result = Shifter_Result;//写入Shifter_Result
	
	//跳转指令
	wire R_jump = R_type&&(func[5:1] == 5'b00100);
	wire [31:0] Rj_JumpAddr = reg_rdata1;//感觉此处并不需要特别地处理func[0]，可能是到后面选择的时候用吧。
	wire Rj_ret = R_jump&func[0];//写入pc_8
	//RF_wdata/wire [31:0] Rj_wdata = pc_8;
	wire R_wen = R_jump?func[0]:R_type;
	
	//mov指令
	wire R_mov = R_type&&(func[5:1] == 5'b00101);
	wire rt_Zero = ~(|reg_rdata2);//如果rt读出数据是0，那么Zero就是1
	wire mov = R_mov&(rt_Zero^func[0]);//此时就可以靠异或来得到是否mov，此处也是在后面用到的信号。
	//RF_wdata/wire [31:0] Rm_wdata = reg_rdata1;//写入reg_rdata1
	
	//2. REGIMM指令
	wire RI = (opcode == 6'b000001);
	wire [17:0] Shifted_imm = {imm,{2{1'b0}}};
	//reg_raddr1/wire [4:0] RI_raddr1 = rs;
	//reg_raddr2/wire [4:0] RI_raddr2 = 5'b00000;
	//alu_A/wire RI_alu_A = reg_rdata1;
	//alu_B/wire RI_alu_B = reg_rdata2;
	wire RI_alu_op = {3{opcode[0]}};
	wire RI_result = RI&alu_Result[0];//只需要取最低位
	wire RI_branch = RI&(RI_result^rt[0]);//仍然使用异或，1就是跳，0就是不跳；
	//wire [31:0] RIB_branch_addr = {{14{imm[15]}},Shifted_imm};
	assign branch_addr = {{14{imm[15]}},Shifted_imm};
	
	//3. J-type指令
	wire J_type = (opcode[5:1] == 5'b00001);
	wire [31:0] J_next_pc = {pc_4[31:28],instr_index,{2{1'b0}}};
	wire J_wen = J_type & opcode[0];
	//RegDst/wire [4:0] J_waddr = 5'b11111;
	//RF_wdata/wire [31:0] J_wdata = pc_8;//写入pc_8
	
	
	//I-type中高度一致的代码合并
	//4. IB-type
	wire IB = (opcode[5:2] == 4'b0001);
	//reg_raddr1/wire [4:0] IB_raddr1 = rs;
	//reg_raddr2/wire [4:0] IB_raddr2 = rt;
	//alu_A/wire [31:0] IB_alu_A = reg_rdata1;
	//alu_B/wire [31:0] IB_alu_B = reg_rdata2;
	wire [2:0] IB_alu_op = {{2{1'b1}},opcode[1]};
	wire IB_eq_branch = (alu_Zero^opcode[0])&!opcode[1];//声明两个跳转标志。
	wire IB_gl_branch = ((alu_Result[0]^opcode[0]) |(~(|reg_rdata1)))&opcode[1]; //考虑zero的情况，因为SLT不保证0的情况
	//same as RI/wire [31:0] IB_branch_addr = {{14{imm[15]}},Shifted_imm};
	
	//5. IC-type
	wire IC = (opcode[5:3] == 3'b001);
	wire IC_wen = IC&opcode[3];
	//reg_raddr1/wire [4:0] IC_raddr1 = rs;
	//RegDst/wire [4:0] IC_waddr = rt;
	wire [31:0] extended_imm = (opcode == 6'b001111)?{imm,{16{1'b0}}}//看看是不是lui
	                           :(opcode[2]?{{16{(!opcode[2])&imm[15]}},imm}:{{16{(!opcode[2])&imm[15]}},imm});//再看是不是扩展,0则为符号扩展
	//alu_A/wire [31:0] IC_alu_A = reg_rdata1;
	//alu_B/wire [31:0] IC_alu_B = extended_imm;
	wire [2:0] IC_alu_op = (opcode[2:0] == 3'b111)?(3'b010):(opcode[2]?{opcode[1],!opcode[2],opcode[0]}//扩展一个lui的
	                       :{!opcode[0],!opcode[2],opcode[1]});//和前面的ALU构造及其相似
	//RF_wdata/wire [31:0] IC_wdata = alu_Result;//写入alu_Result
	
	//6. IL-type 这里之后就是和内存交互了
	wire IL = (opcode[5:3] == 3'b100);
	//相同的代码：
	wire [31:0] ILS_extended_imm = {{16{imm[15]}},imm};
	wire [31:0] ILS_Address = alu_Result;//这里给出内存的地址，接下来就需要对得到的结果进行选择，构造一个掩码。
	wire [31:0] ILS_Address_aligned = {ILS_Address[31:2],{2{1'b0}}};
	wire [31:0] IL_read_data = Read_data;//{Read_data[7:0],Read_data[15:0],Read_data[23:16],Read_data[31:24]};	
	wire [31:0] IL_mask_rl_data = (ILS_Address[1:0]==2'b11 ?(opcode[2]?{reg_rdata2[31:8],IL_read_data[31:24]}:IL_read_data)//前面是从内存中读入lwr，
                                   :(ILS_Address[1:0]==2'b10 ?(opcode[2]?{reg_rdata2[31:16],IL_read_data[31:16]}:{IL_read_data[23:0],reg_rdata2[7:0]})
                                   :(ILS_Address[1:0]==2'b01 ?(opcode[2]?{reg_rdata2[31:24],IL_read_data[31:8]}:{IL_read_data[15:0],reg_rdata2[15:0]})
                                   :(opcode[2]?IL_read_data:{IL_read_data[7:0],reg_rdata2[23:0]}))));
    wire [31:0] IL_mask_b_wdata = (ILS_Address[1:0]==2'b00 ?{{24{!opcode[2]&IL_read_data[7]}},IL_read_data[7:0]}:
                                   (ILS_Address[1:0]==2'b01 ?{{24{!opcode[2]&IL_read_data[15]}},IL_read_data[15:8]}:
                                   (ILS_Address[1:0]==2'b10 ?{{24{!opcode[2]&IL_read_data[23]}},IL_read_data[23:16]}
                                   :{{24{!opcode[2]&IL_read_data[31]}},IL_read_data[31:24]})));
    wire [31:0] IL_mask_h_wdata = (ILS_Address[1:0]==2'b10 ? {{16{!opcode[2]&IL_read_data[31]}},IL_read_data[31:16]}:
                                  {{16{!opcode[2]&IL_read_data[15]}},IL_read_data[15:0]});	
	wire [31:0] IL_wdata = ((IL && opcode[1:0] == 2'b00) ?IL_mask_b_wdata://先看lb和lbu
	                       ((IL && opcode[1:0] == 2'b01) ?IL_mask_h_wdata://再看lh和lhu
	                       (opcode == 6'b100011 ?IL_read_data:IL_mask_rl_data//考虑一下lw
	                       )));
	//reg_raddr1/wire [4:0] IL_raddr1 = rs;//就是base
	//reg_raddr2/wire [4:0] IL_raddr2 = rt;//为lwl做准备
	//RegDst/wire [4:0] IL_waddr = rt;
	//alu_A/wire [31:0] IL_alu_A = reg_rdata1;
	//alu_op/wire [2:0]  IL_alu_op = 3'b010;
	/*wire [31:0] IL_mask_b = (IL_Address[1:0]==2'b11 ?32'h000000ff:
                                   (IL_Address[1:0]==2'b10 ?32'h0000ff00:
                                   (IL_Address[1:0]==2'b01 ?32'h00ff0000:32'hff000000)));*/	
	//7. IS-type
	wire IS = (opcode[5:3] == 3'b101);
	wire [3:0] IS_mask_b_strb = (ILS_Address[1:0]==2'b00 ?4'b0001
                               :(ILS_Address[1:0]==2'b01 ?4'b0010
                               :(ILS_Address[1:0]==2'b10 ?4'b0100
                               :4'b1000)));
	wire [31:0] IS_b_data = (ILS_Address[1:0]==2'b00 ? {{24{1'b0}},reg_rdata2[7:0]}
                               :(ILS_Address[1:0]==2'b01 ?{{16{1'b0}},reg_rdata2[7:0],{8{1'b0}}}
                               :(ILS_Address[1:0]==2'b10 ?{{8{1'b0}},reg_rdata2[7:0],{16{1'b0}}}
                               :{reg_rdata2[7:0],{24{1'b0}}})));
	wire [3:0] IS_mask_h_strb = ILS_Address[1:0]==2'b10 ?4'b1100:4'b0011;
	wire [31:0] IS_h_data = (ILS_Address[1:0] == 2'b10)?{reg_rdata2[15:0],{16{1'b0}}}:{{16{1'b0}},reg_rdata2[15:0]};
	wire [3:0] IS_mask = (ILS_Address[1:0]==2'b11 ?(opcode[2]?4'b1000:4'b1111)//前面是从内存中读入swr，
	                       :(ILS_Address[1:0]==2'b10 ?(opcode[2]?4'b1100:4'b0111)
	                       :(ILS_Address[1:0]==2'b01 ?(opcode[2]?4'b1110:4'b0011)
	                       :(opcode[2]?4'b1111:4'b0001))));
	wire [31:0] IS_rl_data = (ILS_Address[1:0]==2'b11 ?(opcode[2]?{reg_rdata2[7:0],{24{1'b0}}}:reg_rdata2)//前面是swr，
	                       :(ILS_Address[1:0]==2'b10 ?(opcode[2]?{reg_rdata2[15:0],{16{1'b0}}}:{{8{1'b0}},reg_rdata2[31:8]})
	                       :(ILS_Address[1:0]==2'b01 ?(opcode[2]?{reg_rdata2[23:0],{8{1'b0}}}:{{16{1'b0}},reg_rdata2[31:16]})
	                       :(opcode[2]?reg_rdata2:{{24{1'b0}},reg_rdata2[31:24]}))));
	assign Write_data = (opcode[1:0] == 2'b10)?IS_rl_data:
	                     ((opcode[1:0] == 2'b00)?IS_b_data:
	                     ((opcode[1:0] == 2'b01)?IS_h_data:reg_rdata2));
	assign Write_strb = (opcode == 6'b101000 ?IS_mask_b_strb://先看sb
	                       (opcode == 6'b101001 ?IS_mask_h_strb://再看sh
	                       (opcode == 6'b101011 ?4'b1111:IS_mask)));//考虑一下sw
	//reg_raddr1/wire [4:0] IS_raddr1 = rs;//就是base
	//reg_raddr2/wire [4:0] IS_raddr2 = rt;//为lwl做准备
	//alu_A/wire [31:0] IS_alu_A = reg_rdata1;
	//same as IL/wire [31:0] IS_extended_imm = {{16{imm[15]}},imm};
	//alu_op/wire [2:0] IS_alu_op = 3'b010;
	//same as IL/wire [31:0] IS_Address = alu_Result;//这里给出内存的地址，接下来就需要对得到的结果进行选择，构造一个掩码。
	//same as IL/wire [31:0] IS_Address_aligned = {IS_Address[31:2],{2{1'b0}}};
	
	//开始构建数据通路
	assign RegDst = R_type & (R_mov?(mov?1'b1:1'b0):1'b1);//看看哪些是写入rt，哪些是写rd。还有一个0和特殊的31号。取1，取rd，考虑mov指令的选择，要在0寄存器。
	//考虑实际的Write_register, 并注释掉重复写的地方。
	assign RF_waddr = RegDst?rd:(//如果是1，就选rd
	                   (IL || IC)?rt:(//如果是IL和IC，就选rt
	                   (J_type && J_wen)?5'b11111:5'b00000));//如果是JAL，就31，反之则选择0，反正也写不进去。
	                   
	//只有RI或者IB，J是直接跳转。不过还需要更细致一点，毕竟还需要Zero的保证。                   
	assign Branch = (RI & RI_branch) | (IB & (IB_eq_branch | IB_gl_branch));
	//考虑实际的跳转情况，按照之前的构想，我们只需要考虑branch信号和JumpAddr两个信号即可。
	assign NEW_PC = Jump?pc_addr:(Branch?pc_branch:pc_4);
	assign pc_branch = pc_4 + branch_addr;//IB的跳转目的是一致的。
	assign pc_addr = JumpAddr;
	//signle cohere/assign branch_addr = RIB_branch_addr;
	assign Jump = J_type | R_jump;
	assign JumpAddr = J_type?J_next_pc:Rj_JumpAddr;
	
	//Mem_reg_op
	assign MemWrite = opcode[5] & opcode[3];
	assign MemRead = opcode[5] & (~opcode[3]);
	assign RF_wdata = (R_calc || IC_wen)?alu_Result:
	                  (R_shift?Shifter_Result:
	                  ((Rj_ret || J_wen)?pc_8:
	                  (mov?reg_rdata1:IL_wdata)));
	assign RF_wen = (R_wen | J_wen | IC_wen | IL);//特判一个JR
	assign Address = ILS_Address_aligned;
	//assign Write_data = IS_write_data;
	//assign Write_strb = IS_write_strb;
	
	//ALU的解码
	assign ALUSrc = (R_calc | RI | IB);//R_calc, RI, IB 三类指令是R+R；IC, IL, IS三类指令是R+imm;1->2R
	assign reg_raddr1 = rs; // rs的读取是注定的，纵使有的地方用的是base，不过也是用来作为标号的。其实还有SLL的不同
	assign alu_A = reg_rdata1;
	assign reg_raddr2 = RI?5'b00000:rt;//rt确实是注定的。
	wire [31:0] Imm = (IC?extended_imm:ILS_extended_imm);
	assign alu_B = ALUSrc?reg_rdata2:Imm;
	assign ALUop = R_calc?R_alu_op:
	               (RI?RI_alu_op:
	               (IB?IB_alu_op:
	               (IC?IC_alu_op:3'b010)));
	assign alu_ALUop = ALUop;
	
	//Shiter的传入:不需要了。
	/*assign Shifter_A = R_shift_A;
	assign Shifter_B = R_shift_B;
	assign Shifter_Shiftop = R_shiftop;*/
	
endmodule
```
</div>
<!-- {% endraw %} -->